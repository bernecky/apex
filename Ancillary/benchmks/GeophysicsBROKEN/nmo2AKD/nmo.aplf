 dm←d nmo to;t;a;m;p
⍝ nmo performs normal moveout correction to cdp
⍝ gathered seismic data.
⍝ nmo aligns (flattens) primary reflection events in
⍝ a cdp gather.
⍝ d is cdp gathered seismic data with ⍴x traces, each
⍝ trace contains the same arbitrary number of time samples.
⍝ x is a vector whose components are the offsets.
⍝ nmo is accomplished in a matrix manner.
⍝ Vectors to and vstk(to) are supplied externally.
⍝ ⍴to is selected by the user.
⍝ to and vstk must be provided at sample interval dt.
⍝ Interpolation is 4 point, quadratic least square error.
⍝ -------------------------------------------------------
 to[0]←1 ⍝ Avoid divide by zero in outer product
⍝⍝t←(((⍴x),⍴to)⍴to)×4○x∘.÷to×vstk
 t←(4○x∘.÷to×vstk)×⍤1⊢to
 t[;0]←⌈/(2×dt),x÷vstk
 a←(dt|t)÷dt
 m←STSCfloor t÷dt
⍝ KLUDGE: We can't determine shape of ⌈/m[;¯1+⍴x]
⍝ because we don't carry partial shape information.
⍝ Hence, we rewrite the next line, to make the
⍝ reshape on d (which has to know left argument shapes)
⍝ work.
⍝⍝⍝p←0⌈(⌈/m[¯1+⍴x;])+4-⍴to
 p←0⌈(⌈/m[¯1+(⍴x)[0];])+4-(⍴to)[0] ⍝ KLUDGE
 d←d,((⍴x),p)⍴0
 dm←(a×1-a)×(d fr1 m+1)+d fr1 m-2
⍝⍝dm←(a×1-a)×(d index m+1)+d index m-2
 dm←0.25×((a×5-a)×d fr1 m)+((1-a)×(a+4)×d fr1 m-1)-dm
