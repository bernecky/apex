 r←tomcatv n;x;y;nrx;nry;aa;d;dd;rx;ry;ll;irxm;jrxm;rxm;irym;jrym;rym;i;j;rx1;ry1;sr;junk;loops
⍝ Tomcatv ala John Feo's SISAL 1.2 algorithm
 x←datax n ⍝ x,y transposed from f77
 y←x datay n
 irxm←jrxm←irym←jrym←0
 rxm←rym←0
 i←666
⍝⍝⍝ll←¯1
⍝⍝⍝:while 100>ll←ll+1
 loops←100
 :For ll :In ⍳loops
⍝⍝  inlined compmesh
     ⍝⍝ ry←x compmesh y ⍝ n is global in; aa,dd,rx,ry out
⍝⍝    r←x compmesh y;xx;yx;xy;yy;a;b;c;pxx;pxy;qxx;qxy;pyy;qyy;ry

     xx←(2 1↓0 ¯1↓x)-0 1↓¯2 ¯1↓x
     yx←(2 1↓0 ¯1↓y)-0 1↓¯2 ¯1↓y
     xy←(1 2↓¯1 0↓x)-1 0↓¯1 ¯2↓x
     yy←(1 2↓¯1 0↓y)-1 0↓¯1 ¯2↓y
⍝
     a←0.25×(xy×xy)+yy×yy
     b←0.25×(yx×yx)+xx×xx
     c←0.125×(xy×xx)+yx×yy
⍝
     pxx←(2 1↓0 ¯1↓x)-(2×1 1↓¯1 ¯1↓x)-0 1↓¯2 ¯1↓x
     qxx←(2 1↓0 ¯1↓y)-(2×1 1↓¯1 ¯1↓y)-0 1↓¯2 ¯1↓y
     pyy←(1 2↓¯1 0↓x)-(2×1 1↓¯1 ¯1↓x)-1 0↓¯1 ¯2↓x
     qyy←(1 2↓¯1 0↓y)-(2×1 1↓¯1 ¯1↓y)-1 0↓¯1 ¯2↓y
⍝
     pxy←((2 2↓x)-2 ¯2↓x)+(¯2 ¯2↓x)-¯2 2↓x ⍝ signs
     qxy←((2 2↓y)-2 ¯2↓y)+(¯2 ¯2↓y)-¯2 2↓y
⍝ GLOBALS OUT:
     aa←-b
     dd←b+b+(2÷0.98)×a
     rx←(a×pxx)+(b×pyy)-c×pxy
     r←(a×qxx)+(b×qyy)-c×qxy
     ry←r

 ⍝ Find maximum residual & its location
     rxm←fma rx
     irxm←i
     jrxm←j
     rym←fma ry
     irym←i
     jrym←j
 ⍝ Solve tridiagonal system
⍝⍝  inlined
     ⍝⍝ d←aa sweepup dd
 ⍝⍝   d←aa sweepup dd;j;r
⍝ We thought this was a scan, but I can't make it work.
     d←dd
     d[;0]←÷d[;0]
     nrx←rx
     nry←ry
     :For j :In 1+⍳(⍴dd)[1]-1
         r←aa[;j]×d[;j-1]
         d[;j]←÷d[;j]-r×aa[;j-1]
         nrx[;j]←nrx[;j]-nrx[;j-1]×r
         nry[;j]←nry[;j]-nry[;j-1]×r
     :EndFor
 ⍝ sweepup uses x,y, sets nrx,nry
⍝⍝  inlined sweepdn

⍝   j←aa sweepdn dd;r;lim
⍝ Updates nrx,nry
     lim←¯1+(⍴dd)[1]
     nrx[;lim]←nrx[;lim]×dd[;lim]
     nry[;lim]←nry[;lim]×dd[;lim]
⍝
     :For j :In ⌽⍳lim
         nrx[;j]←(nrx[;j]-(aa[;j]×nrx[;j+1]))×dd[;j]
         nry[;j]←(nry[;j]-(aa[;j]×nry[;j+1]))×dd[;j]
     :EndFor
     ⍝   junk←aa sweepdn d ⍝ sweepdn uses &sets nrx,nry
 ⍝ Add corrections
     x←nrx correct x
     y←nry correct y
 :EndFor
⍝⍝:until .5E¯7>(|rxm)⌈|rym  ⍝ No :until support yet
⍝⍝⍝⍝lz:r←(loops+1),(2+irxm,jrxm),rxm,(2+irym,jrym),rym
 r←(+/,x)++/,y
⍝∇⍣§/home/apex/apexgit/Ancillary/benchmksPerf/tomcatvInlined/tomcatv.ufn§0§ 2020 6 24 17 4 23 40 §rÜáòH§0
