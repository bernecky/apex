 r←EmitDyadicOuterProducts fns;cv;fns2;sis;ctl;cvm;fnm
 sis←''
 :If ~0∊⍴fns
     ctl←7 FragmentCode'jotdot'
     cvm←<\((Raze⍉fns[;fnslop,fnsfn])∘.≡ctl[;ctlfn])∧ctl FindRankGen fns
     cv←∨/cvm
     :If 1∊cv
         sis←ctl Getsis cvm
         fns2←cv⌿fns
         fns2[;fnsfn]←fns2[;fnsrop] ⍝ dot now G in ∘.G
         fns2[;fnslop,fnsrop]←E''
         fns2[;fnsranks]←ER1'0',0 1↓D fns2[;fnsranks]
         fns2[;fnscomtyp]←ER1 PFATct PFATProperty fns2
         fnm←Raze⍉(cv⌿fns)[;fnslop,fnsfn,fnsrop,fnstypes]
         sis←(E'$FNAME')Strepl¨ER1(⍪sis),⍪fnm
         sis←sis EmitPrimitiveFnDefnsTemplateFiller fns2 ⍝ Outer product loop
         sis←sis InsertSysvarHeader GenSysvarArgs fns2
         fns←(~cv)⌿fns
     :EndIf
 :EndIf
 r←(E sis),E fns
