 z←b ls2 a;i;j;sigma;alfa;aii;pp;pi;np;factor;eps;mv;m;n;u;ta;tb;tc;td;p;sink;t1;tc1;tc2;td1;td2;newta;newtb;tmp
⍝ M.A. Jenkins.
⍝ IBM Tech Report 320-2989
⍝ Modified for formatting and
⍝ to avoid use of globals, glueing.
⍝ Modified further to run a wee bit faster 1996-03-20 /rbe
⍝ Householder triangularization
⍝ of m×n matrix for linear
⍝ systems and least squares.
⍝ Does row, column interchanges,
⍝ scales columns near unity.
⍝ assumes (⍴⍴a)=(⍴⍴b)=2
⍝ Modified 1995-05-22 to be origin 0/rbe
 p←1↓⍴b
 m←1↑⍴a
 n←¯1↑⍴a
 pp←0+⍳n ⍝ KLUDGE to avoid typeproblems in dfa
 factor←÷⌈⌿|a÷⍉(⌽⍴a)⍴mv←⌈/|a ⍝ FUNCTION RANK!
 a←a×(⍴a)⍴factor ⍝ Function rank!
 np←n+p
 eps←(16*¯13)×⌈/mv
 :For i :In ⍳n
     mv←⌈⌿|(i,i)↓a
     sink←Singular eps≤⌈/mv
     pi←i+mv⍳⌈/mv
     tmp←pp[i]
     pp[i]←pp[pi]
     pp[pi]←tmp
     tmp←a[;i]
     a[;i]←a[;pi]
     a[;pi]←tmp
     pi←i+(|i↓a[;i])⍳⌈/|i↓a[;i]
     tmp←a[i;]
     a[i;]←a[pi;]
     a[pi;]←tmp
     tmp←b[i;]
     b[i;]←b[pi;]
     b[pi;]←tmp
     sigma←+/(i↓a[;i])*2
     aii←a[i;i]
     alfa←(1-2×0≤aii)×sigma*0.5
     tmp←i↓a[;i]
     tmp[0]←tmp[0]-alfa
     newta←tmp+.×(i+0 1)↓a
     newtb←tmp+.×(i,0)↓b
     tb←÷sigma-aii×alfa
     tc1←tmp∘.×tb×newta
     tc2←tmp∘.×tb×newtb
     td1←((i+0 1)↓a)-tc1
     td2←((i,0)↓b)-tc2
     a[i↓⍳m;(i+1)↓⍳n]←td1
     b[i↓⍳m;]←td2
     a[i;i]←alfa
 :EndFor
⍝
 z←(n,p)⍴0
⍝
 :For i :In ⌽⍳n
     t1←b[i;]-a[i;]+.×z
     z[i;]←t1÷a[i;i]
 :EndFor
⍝
 z[pp;]←z
 z←z×⍉(⌽⍴z)⍴factor ⍝ Function rank!
⍝∇⍣§/home/apex/apexgit/TestsForNow/BMBROKEN/mdiv2AKD/ls2.ufn§0§ 2020 6 24 17 4 22 864 §mûjÈú§0
