 r←testis n;i;v;M2;z;M234;k
⍝ Indexed assign tests
 r←(n,n,n)⍴⍳n×n×n
 r←r≡r[;;]                      ⍝ M[;;]
 v←⍳4
 v[0]←23
 r←r,k←v≡v[]                      ⍝ V[]
 ⎕←'V[]: ',ok k
 r←r,k←v≡23 1 2 3                 ⍝ V[S]←S
 ⎕←'V[S]<-S: ',ok k
 v[1 2 2 3]←9 8 7 6
 r←r,k←v≡23 9 7 6                 ⍝ V[Vdups]←V
 ⎕←'V[Vdups]<-V: ',ok k
 v[1 2]←10
 r←r,k←v≡23 10 10 6               ⍝ V[V]←S
 ⎕←'V[V]<-S: ',ok k
 v[]←⌽⍳4
 r←r,k←v≡3 2 1 0                  ⍝ V[]←V
 ⎕←'V[]<-V: ',ok k
 v[]←11
 r←r,k←v≡11 11 11 11              ⍝ V[]←S
 ⎕←'V[]<-S: ',ok k
⍝ Rank-2 tests
 M2←2 3⍴⍳6
 M2[;]←42                       ⍝ M[]←S
 r←r,k←M2≡2 3⍴42
 ⎕←'M[]<-S: ',ok k
 M2←2 3⍴⍳6
 M2[0;]←55                      ⍝ M[S;]←S
 r←r,k←M2≡2 3⍴55 55 55 3 4 5
 ⎕←'M[S;]<-S: ',ok k
 M2←2 3⍴⍳6
 M2[;]←⌽M2                      ⍝ M[;]←M
 r←r,k←M2≡2 3⍴2 1 0 5 4 3
 ⎕←'M[;]<-M: ',ok k
 M2←2 3⍴⍳6
 M2[0;]←7 8 9                   ⍝ M[S;]←V
 r←r,k←M2≡2 3⍴7 8 9 3 4 5
 ⎕←'M[S;]<-V: ',ok k
 M2←2 3⍴⍳6
 M2[;0]←10 20                   ⍝ M[;S]←V
 r←r,k←M2≡2 3⍴10 1 2 20 4 5
 ⎕←'M[;S]<-V: ',ok k
 M2←2 3⍴⍳6
 M2[1 0;]←2 3⍴10 20 30 40 50 60 ⍝ M[V;]←M
 r←r,k←M2≡2 3⍴40 50 60 10 20 30
 ⎕←'M[V;]<-M: ',ok k
 M2←2 3⍴⍳6
 M2[0;1]←666                    ⍝ M[S;S]←S
 r←r,k←M2≡2 3⍴0 666 2 3 4 5
 ⎕←'M[S;S]<-S: ',ok k
 M2[1 0;2 1]←42                ⍝ M[V;V]←S
 r←r,M2≡2 3⍴0 42 42 3 42 42
 ⎕←'M[V;V]<-S: ',ok k
 M2[1 0;2 0]←2 2⍴9 8 7 6      ⍝ M[V;V]←M
 r←r,k←M2≡2 3⍴6 42 7 8 42 9
 ⎕←'M[V;V]<-M: ',ok k
⍝ Rank-3 tests
 M234←2 3 4⍴⍳24
 M234[0;;]←3.5                  ⍝ M[S;;]←S
 r←r,k←M234≡3.5⍪1 0 0↓2 3 4⍴⍳24
 ⎕←'M[S;;]<-S: ',ok k
 M234[0;;]←3 4⍴12               ⍝ M[S;;]←M
 r←r,k←M234≡12⍪1 3 4⍴12+⍳12
 ⎕←'M[S;;]<-M: ',ok k
 M234[1 0;;]←42                 ⍝ M[V;;]←S
 r←r,k←M234≡2 3 4⍴42
 ⎕←'M[V;;]<-S: ',ok k
 M234←2 3 4⍴⍳24
 M234[2 1⍴1 0;;]←42             ⍝ M[M;;]←S
 r←r,k←M234≡2 3 4⍴42
 ⎕←'M[M;;]<-S: ',ok k
 M234←2 3 4⍴⍳24
 v←M234
 v[0;2;]←42
 v[1;2;]←42
 M234[;2;]←42                   ⍝ M[;S;]←S
 r←r,M234≡v
 ⎕←'M[;S;]<-S: ',ok k
 M234←2 3 4⍴⍳24
 M234[;;3]←42                   ⍝ M[;;S]←S
 r←r,k←M234≡(0 0 ¯1↓2 3 4⍴⍳24),42
 ⎕←'M[;;S]<-S: ',ok k
 M234←2 3 4⍴⍳24
 M234[0;0;2]←42                 ⍝ M[S;S;S]←S
 r←r,k←(,M234)≡0 1 42,3↓,2 3 4⍴⍳24
 ⎕←'M[S;S;S]<-S: ',ok k
 M2←3 6⍴⍳99
 M2[;3 4 5]←3 3⍴100+⍳9          ⍝ M[;V]←M
 r←r,k←M2≡(3 3⍴0 1 2 6 7 8 12 13 14),3 3⍴100+⍳9
 ⎕←'M[;V]<-M: ',ok k
